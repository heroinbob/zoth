defmodule Zoth.OpenId.ClaimTest do
  use ExUnit.Case, async: true

  alias Zoth.OpenId.Claim
  alias Zoth.Test.Fixtures

  describe "get_value_for/2" do
    test "returns the value for the claim from the source" do
      source = %{united_states_of: :whatever}
      claim = Fixtures.build(:open_id_claim, name: :united_states_of)

      assert Claim.get_value_for(claim, source) == :whatever
    end

    test "relies on the alias when defined" do
      source = %{yuk: :dum}

      claim =
        Fixtures.build(
          :open_id_claim,
          alias: :yuk,
          name: :fail
        )

      assert Claim.get_value_for(claim, source) == :dum
    end

    test "returns nil when the source does not have the attr" do
      source = %{united_states_of: :whatever}
      claim = Fixtures.build(:open_id_claim, name: :california, value_when_missing: :state)

      assert Claim.get_value_for(claim, source) == :state
    end

    test "returns the provided default when the source doesn't have the attr" do
      source = %{united_states_of: :whatever}
      claim = Fixtures.build(:open_id_claim, name: :california)

      assert Claim.get_value_for(claim, source) == nil
    end

    test "returns the transformer result when :transformer is given as a function" do
      source = %{united_states_of: :whatever}

      # If you pass a function to ex machina then it'll execute it which isn't what we want.
      claim =
        :open_id_claim
        |> Fixtures.build(name: :united_states_of)
        |> Map.put(:transformer, &Atom.to_string(&1.united_states_of))

      assert Claim.get_value_for(claim, source) == "whatever"
    end

    test "ignores :transformer when it's not a function" do
      source = %{united_states_of: :whatever}

      claim =
        Fixtures.build(
          :open_id_claim,
          name: :united_states_of,
          transformer: :robots_in_disguise
        )

      assert Claim.get_value_for(claim, source) == :whatever
    end
  end

  describe "new/1" do
    test "returns a claim from the given map" do
      assert Claim.new(%{name: :foo}) == %Claim{
               alias: nil,
               includes: [],
               name: :foo
             }

      assert Claim.new(%{alias: :baz, name: :foo}) == %Claim{alias: :baz, name: :foo}

      assert Claim.new(%{
               includes: [%{name: :nested}],
               name: :foo
             }) == %Claim{
               name: :foo,
               includes: [
                 %Claim{name: :nested}
               ]
             }
    end
  end
end
